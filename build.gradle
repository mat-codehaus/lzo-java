buildscript {
	repositories {
		// mavenLocal()
		mavenCentral()
		// jcenter()
		// maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		gradlePluginPortal()
	}

	dependencies {
		classpath 'org.anarres.gradle:gradle-stdproject-plugin:1.0.10'
		classpath 'org.anarres.gradle:gradle-jcpp-plugin:1.0.2'
	}
}

ext.githubProjectName = 'lzo-java'

apply plugin: 'org.anarres.stdproject'
stdproject {
}

subprojects {

	// Override version if a project property is specified
	if (project.hasProperty('releaseVersion')) {
		version = project.releaseVersion
	}

	group = "org.anarres.lzo"

	apply plugin: 'org.anarres.stdmodule'
	stdmodule {
		description "The LZO compression codec in pure Java."
		author id: 'shevek', name: 'Shevek', email: 'github@anarres.org'
		license 'GPL-3.0'
	}

	dependencies {
		compile 'com.google.code.findbugs:annotations:2.0.3'

		testCompile 'org.slf4j:slf4j-api:1.7.7'
		testCompile 'commons-io:commons-io:2.4'
	}

	sourceCompatibility = 1.5
}

project(':lzo-core') {
	apply plugin: 'jcpp'

	dependencies {
		compile 'commons-logging:commons-logging:1.1.3'
	}

	jcpp {
		localIncludePath << project.file("src/main/jcpp/include")
		localIncludePath << project.file("src/main/jcpp/src")
	}

	jar {
		manifest {

			attributes('Bundle-ManifestVersion': '2')
			attributes('Bundle-Name': 	         'LZO Compression')
			attributes('Bundle-Version':         "${version}")
			attributes('Bundle-SymbolicName':    'org.anarres.lzo')
			attributes('Export-Package': 	     'org.anarres.lzo;version="1.0.7"')
			attributes('Import-Package': 	     'org.apache.commons.logging')
		}
	}

	task bundleJars(type: Copy) {
		// Define the directory to store the bundled JARs
		def bundleDir = "${buildDir}/bundle"
		mkdir(bundleDir) // Ensure the directory exists

		// Copy the main JAR file
		from jar.outputs.files
		into bundleDir

		// Copy all compile dependency JARs without extracting them
		from configurations.compile
		into "${bundleDir}/libs"

		// Optionally, log the output directory after the task completes
		doLast {
			println "Bundled JARs have been copied to: $bundleDir"
		}
	}
}

project(':lzo-commons') {
	dependencies {
		compile project(':lzo-core')
		compile 'org.apache.commons:commons-compress:1.13'

		testCompile project(':lzo-core').sourceSets.test.output
	}
}

project(':lzo-hadoop') {
	dependencies {
		compile project(':lzo-core')
		compile 'org.apache.hadoop:hadoop-core:1.2.1'

		testCompile project(':lzo-core').sourceSets.test.output
	}
}
